buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()
}

dependencies {
    compile 'com.google.android:support-v4:r7'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libs:common')
    //compile project(':libs:ViewPager-Indicator')
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate.toInteger()
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"
    defaultConfig {
        targetSdkVersion 19
        minSdkVersion 15
        versionName "build-${getDate()}"
    }
    signingConfigs {
        debug {
            storeFile file(System.getProperty("user.home") + "/.android/debug.keystore")
        }

        release {
            // Save password to ~/.gradle/gradle.properties
            storeFile file(System.getProperty("user.home") + MC_ANDROID_STORE_PATH)
            storePassword MC_ANDROID_STORE_PASSWORD
            keyAlias "gojuon"
            keyPassword MC_ANDROID_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            //runProguard true
            //proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }
            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
            assets {
                srcDir 'assets'
            }
            resources {
                srcDir 'src'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

// https://stackoverflow.com/questions/24668746
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

